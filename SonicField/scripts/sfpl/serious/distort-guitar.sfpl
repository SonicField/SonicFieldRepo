"A5" Note Println !pitch
(
	(20000,?pitch)SinWave                         pcnt-90,
	(20000 WhiteNoise,(?pitch,5)*,3)DirectLowPass pcnt-10
)Mix !input !signal

[(((0,1),(1000,-18),(20000,-60))SimpleShape,>input)Multiply !signal]

(((0,1),(20000,0))NumericShape,>input)Multiply !signal

[ Main guitar shaper ]
(
	1,
	0,
	1,
	0,
	1,
	10,
	(
		1,
		0,
		1,
		0,
		1,
		10,
		>signal
	) WaveShaper
) WaveShaper !signal

[ Inject roughness in the pick ]
(
	(
		(1000,?pitch)/ WhiteNoise,
		?pitch,
		1
	)DirectLowPass Pcnt+20,
	>signal        Pcnt+80
)Mix !signal

[ Put in some twang ]
(
	(0,(?pitch,((0.5,Random)-,2)*)+),
	(?signal Length,?pitch)
)Slide !ring
(((0,0),(?ring Length,-99))SimpleShape,?ring)Multiply !ring
(?ring Normalise,?signal Normalise)Multiply !ring
(
	>ring   Pcnt+10,
	>signal Pcnt+90
)Mix !signal

[ Pull up the harmonics ] 
(>signal,(?pitch,2)*,2)DirectHighPass Normalise !signal

[ Envolope the pick ]
(
	(0,0),
	((1000,?pitch)/,1),
	((2000,?pitch)/,1),
	(?signal Length,1)
)NumericShape !shape
(>signal,>shape)Multiply Normalise !signal

[ Do some ditortion ]

?signal !distort

[ Soft Clip ]
(
	(?distort,0.75)Power Normalise dbs+12 Clip pcnt+25,
	>distort pcnt+74
)Mix !distort
(
	 0,0.59,0,-1.2,0.25,1,
	(0,0.59,0,-1.2,0.00,1,?distort)WaveShaper
) WaveShaper !distort


[ Mix with original ]
(
   >distort pcnt+50,
   >signal  pcnt-50,
)Mix Normalise  !signal


[DC Filter]
(?signal,50,1)DirectHighPass          !signal
>signal Normalise !Signal

[ Get the results of the tasks and make a wav file ]
((?signal),"c:\temp\tone.wav")WriteFile16

[ Play that wave file ]
("c:\temp\tone.wav","Primary Sound Driver" Mixer)PlayFile LineWait
