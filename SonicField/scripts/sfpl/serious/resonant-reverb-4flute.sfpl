[Play a sample]
{
	((?left,?right),"c:\temp\tone.wav")Sf.WriteFile16
	("c:\temp\tone.wav","Primary Sound Driver" Sf.Mixer)Sf.PlayFile sf.LineWait
} _play

{
	(?sample,1000,5)SFFilter.NLowPass  !sampleL
	(?sample,3000,2)SFFilter.NLowPass  !sampleN
	(?sample,1000,4)SFFilter.NHighPass !sampleH
	(?sample,((?first ,?offset)Sum Maths.Prime Println SF.Silence,(?sample,-13)SF.Volume)SF.Concatenate)SF.Add    !wet
    (
    	1,2,
	    {
	    	!z
			(>first,1.2)Prod Maths.Prime !first
	    	(?wet,(((?first,30)Diff,?offset)Diff Println SF.Silence,(?sampleL,(-10,?z)Diff)SF.Volume)SF.Concatenate)SF.Add   !wet
	    	(?wet,(((?first,30)Sum ,?offset)Diff Println SF.Silence,(?sampleN,(-6,?z )Diff)SF.Volume)SF.Concatenate)SF.Add   !wet
	    	(?wet,((?first         ,?offset)Diff Println SF.Silence,(?sampleH,(-10,?z)Diff)SF.Volume)SF.Concatenate)SF.Add   !wet
	    }
	)Repeat
	(
    	3,14,
	    {
	    	!z
			(>first,1.2)Prod Maths.Prime !first
	    	(?wet,(((?first,30)Diff,?offset)Diff Println SF.Silence,(?sampleL,(-7,?z)Diff)SF.Volume)SF.Concatenate)SF.Add    !wet
	    	(?wet,(((?first,30)Sum ,?offset)Diff Println SF.Silence,(?sampleN,(-4,?z)Diff)SF.Volume)SF.Concatenate)SF.Add    !wet
	    	(?wet,((?first         ,?offset)Diff Println SF.Silence,(?sampleH,(-7,?z)Diff)SF.Volume)SF.Concatenate)SF.Add    !wet
	    }
	)Repeat
	(>wet,-3)SF.Volume !wet
} _earlyReflects

{
    [A mixture of a soft and harder filter and different frequences]
	(2,250)SF.SinWave SFAMP.MakeSawTooth     !shapeA
	(1,500)SF.SinWave                        !shapeB
	(>shapeA,>shapeB)SF.Add SF.NormaliseArea !shape
	(?shape, -4)SF.Volume                    !shape
	
	(>wet,1000 SF.Silence)SF.Concatenate                  !wet,!dry
	
	(
		?wet,
		(		
			(((?shape,2.11 )SFFilter.DirectResample,-7.1)SF.Volume,?first ),
			(((?shape,1.13 )SFFilter.DirectResample,-2.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first),
			(((?shape,0.51 )SFFilter.DirectResample,-3.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first),
			(((?shape,3.23 )SFFilter.DirectResample,-1.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first),
			(((?shape,1.21 )SFFilter.DirectResample,-2.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first),
			(((?shape,0.91 )SFFilter.DirectResample,-3.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first),
			(((?shape,4.5  )SFFilter.DirectResample,-4.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first),
			(((?shape,2.47 )SFFilter.DirectResample,-5.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first),
			(((?shape,3.07 )SFFilter.DirectResample,-6.0)SF.Volume,(>first,1.3) Prod Maths.Prime !first)
		)
	)SF.Resonate                                   !wet
	(>wet SF.Invert,?dry)SF.Add                    !wet
	(>wet,(>dry,-3)SF.Volume)SF.Add SF.Normalise   !wet
	(?predelay SF.Silence,>wet)SF.Concatenate      
} _laterReflections

[ Read the input ]
"C:\temp\tone-in.wav"  !wavFile
?wavFile Sf.ReadFile            !channels
?channels ListHead              !leftIn
>channels ListTail ListHead     !RightIn
[((2,500)SF.SinWave SFAMP.MakeTriangle,3000 SF.Silence)SF.Concatenate !sample
(?sample,>sample)SF.Concatenate !leftIn !rightIn]

[ Get a mix of the sample and the early reflections ]
75                !first
0                  !predelay
?leftIn            !sample
10                 !offset
_earlyReflects Do  !left
?rightIn           !sample
-10                !offset
_earlyReflects Do  !right
>sample

?left Done         !left
?right Done        !right

[ Get just the reflects ]
{(>left ,?leftIn  SF.Invert)SF.Add}Do !left
{(>right,?rightIn SF.Invert)SF.Add}Do !right
>left  Done !left
>right Done !right

[Cross mix a bit]
((>left,(?right,-12)SF.Volume)SF.Add,-1)SF.Volume !left
((>right,(?left,-12)SF.Volume)SF.Add,-1)SF.Volume !right

[ Now we have sample, left and right 
  Feed the earlies in the reverb     ]
75 !first
>left                !wet
_laterReflections Do !left
>right               !wet
_laterReflections Do !right

>left  Done          !left
>right Done          !right

[ At this point left and right have the earliest and the reverb ]
{(>left ,(?leftIn ,1)SF.Volume)SF.Add SF.Normalise}Do !left
{(>right,(?rightIn,1)SF.Volume)SF.Add SF.Normalise}Do !right
>left  Done          !left
>right Done          !right

_play Do Done