[
********************************
          Closures
          ========
********************************
]
{
    ?signal Length !length
    Semitone       !step
      120          !bands
       "C0" Note   !pitch
    bunch          !m-channels
    1 Silence !output-signal 
    (>signal,2)DirectRelength !s-in
    (
        1,?bands,
        {
            ("Scanning channel ",?pitch)Println
            (?pitch,2)/  !p-use
            (?length,2)/ !l-use
            (?pitch,8)*  !p-use-l
            (?length,8)* !l-use-l
            {
                (?s-in,  ?p-use,0.1,18)RBJPeaking dbs-18 WaveLimit !signal
                (?signal,?p-use,0.1,18)RBJPeaking dbs-18 WaveLimit !signal
                (?signal,?p-use,0.1,18)RBJPeaking dbs-18 WaveLimit !signal
                [(?signal,?p-use,0.1,18)RBJPeaking dbs-18 WaveLimit !signal]
                (?signal,?p-use,0.1,18)RBJPeaking dbs-18 WaveLimit !chan-sig
                (?p-use period,2)/                          !pa
                (?p-use period,2)*                          !pr
                (?chan-sig,?pa,?pr)Follow WaveLimit         !chansig
                (>chan-sig,(?p-use,2)/,4)ButterworthLowPass !chan-sig
                [ Now enhance it ]
                (>chan-sig,?enhance-channels)Power !chan-sig
                [ Now gate it]
                (
                    (
                        (?chan-sig,?gate)Gate,
                        50,
                        4
                    )ButterworthLowPass,
                    >chan-sig
                )Multiply
            }Do !chan-sig
                        
            (?chan-sig,(1,16)/)DirectRelength !chan-sig
            (>p-use,2)/ !p-use
            {    
                ("Regenerating channel ",?pitch)Println
                (
                    (?p-use,440)Lt,
                    {
                        (
                            (?l-use-l,(?p-use,2)/)SinWave               pcnt+25,
                            (?l-use-l,(?p-use,4)/)SinWave               pcnt+25,
                            (?l-use-l,?p-use)SinWave                    pcnt+50
                        )Mix !wave
                    },
                    {
                        [ Should add brightness to the louder bits ]
                        (
                            (?l-use-l,(?p-use,2)*)SinWave               pcnt+25,
                            (?l-use-l,?p-use)SinWave                    pcnt+75
                        )Mix !wave
                    }
                )Choose Invoke
                >wave
            }Do !wave

            (
                >wave,
                ?chan-sig
            )Multiply !signal
                         
            (
                >signal,
                ,
                (
                    (>chan-sig dbs+1,2)Power,
                    (?l-use-l,(?p-use,8)*)SinWave
                )Multiply
            )Mix  RemoveDc !signal 
            
            (
                >signal,
                >output-signal
            )Mix RemoveDc !output-signal
            (>pitch,?step)* !pitch
        }
    )Repeat
    >output-signal
}!vocode


{
    >divisor   ReadIn !divisor
    >dividend  ReadIn !dividend
    >beater    ReadIn !beater
    >volume    ReadIn !volume
    (?x,?beat-in)* !x
    (?x,?rate)*    !y
    (1,(10,((?divisor,?y)ValueAt, 1)+)*)+ Integer  !divisor-number
    (1,(10,((?dividend,?y)ValueAt,1)+)*)+ Integer  !dividend-number
    (1,(?beat-multi,((?beater  ,?x)ValueAt,1)+)*)+ Integer !beat 
    (0.5,(0.5,(?volume,?x)ValueAt)*)+              !volume-number 
    (?dividend-number,?divisor-number)/            !multiplier
    (?multiplier,?root)*                           !pitch 
    (?pitch,(4,?beat)*)*                           !saw-cut
    (?beat-in,(?beat,2)*)*                         !beat
    >divisor   
    >dividend 
    >beater    
    >volume 
    
    (?bass Do Done,?volume-number)NumericVolume   !note
    
}!make-note
{
    32 !root
    ?make-note Do Done
}!make-note-l
{
    512 !root
    ?make-note Do Done
}!make-note-h

[ 
********************************
        Main code
        =========
********************************
]

[ Generator Params ]
200000 !total-length
900    !beat-in
0.05  !rate
2      !dullness
36     !space
4      !beat-multi

[ Vocoder Params ]
1.250          !enhance-channels
0.0001         !gate

[ Generate encoding waves ]
(?total-length,(2,0.5)**,Random)PhasedSinWave       !divisor
(?total-length,(3,0.5)**,Random)PhasedSinWave       !dividend
(?total-length,(1.5,0.5)**,Random)PhasedSinWave     !beater
(?total-length,(2,(1,3)/)**,Random)PhasedSinWave    !stereo
(
    ?total-length,
    (
        (3,(1,3)/)**,
        ,2
    )/
)SinWave !volume

