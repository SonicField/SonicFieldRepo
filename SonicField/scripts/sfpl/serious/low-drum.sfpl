[Play a sample]
{
	((?sample),"c:\temp\tone.wav")Sf.WriteFile16
	("c:\temp\tone.wav","Primary Sound Driver" Sf.Mixer)Sf.PlayFile sf.LineWait
} _play
{
	"Starting Drum" PrintLn   
	(1000 SF.WhiteNoise)SF.Concatenate                       !sample
	(
		((0,1),(500,-16),(1000,-36))SFShape.Simple,
		>sample
	)SF.Multiply                                             !sample
	(?sample,200,2)SFFilter.NLowPass SF.Normalise            !sample
	((0,1),(0.5,5),(10,0))SFShape.Numeric SF.NormaliseArea   !shape
	(>shape,1.0)SF.Volume                                    !shape
	(
		?sample,
		(		
			((?shape,-5 )SF.Volume,10),
			((?shape,-10 )SF.Volume,20),
			((?shape,-10)SF.Volume,30),
			((?shape,-3)SF.Volume,31.4),
			((?shape,-8)SF.Volume,62.8),
			((?shape,-8)SF.Volume,94.2)
		)
	)SF.Resonate SF.Normalise                                !sample
	((?sample,1,1)SFFilter.NHighPass,3.0)SF.Volume           !sampleB
	(>sample SF.Invert,>sampleB)SF.Add SF.Normalise          !sample
	"Done Drum" PrintLn   
	>sample
} _bass-drum

List !drumDo
(
	1,10,
	{
		(_bass-drum Do,>drumDo)ListAddEnd !drumDo
	}
)Repeat

List !drum
(
	1,60,
	{
		(?drumDo ListHead Done,?drum)ListAddEnd !drum
		>drumDo  ListTail !drumDo 
	}
)Repeat
>drum SF.Concatenate !sample

_play Do Done