[Play a sample]
{
	((?sample),"c:\temp\tone.wav")Sf.WriteFile16
	("c:\temp\tone.wav","Primary Sound Driver" Sf.Mixer)Sf.PlayFile sf.LineWait
} _play
{
	[ Parameters:
	  len     - length of note.
	  mix     - percent of resonant signal
	  pitch   - the pitch around which the drum is based 
	  clatter - high pitch boost
	]
	
	"Starting Drum" PrintLn
	[
	 hand tuned noise envelope to make the drive for the drum
	]   
	(?len SF.WhiteNoise)SF.Concatenate                       !sample
	(
		((0,1),((?len,0.5)Prod,-16),(?len,-36))SFShape.Simple,
		>sample
	)SF.Multiply                                             !sample
	
	[
	 The input noise is split into bands to agitate the radial and
	 circular resonance.
	]
	(
		?sample,
		(?pitch,1.5)Quot,
		(?pitch,0.75)Quot,
		2
	)SFFilter.NBandPass SF.Normalise                          !rSample
	(?pitch,Maths.PI)Quot                                     !cPitch
	(
		?sample,
		(?cPitch,1.5)Quot,
		(?cPitch,0.75)Quot,
		2
	)SFFilter.NBandPass SF.Normalise                          !cSample

	(
		(>rSample, 0.0)SF.Volume,
		(>cSample, 0.0)SF.Volume
	)SF.Add SF.Normalise                                     !sample
	(>sample,(?pitch,6)Prod,1)SFFilter.NLowPass              !drive    !sample
	
	((0,0),(5,1),(10,0))SFShape.Numeric                      !shape
	 [
	  Black magic!
	  ============
	  
	  The setting for the shape of the convolution is simply
	  hand tunned to make the drum sound nice.
	 ]
	(>shape,(?pitch,150)Quot)SFFilter.DirectResample         !shape
	>shape SF.NormaliseArea                                  !shape
	(>shape,2.2)SF.Volume                                    !shape
	(1000,(?pitch,1.5)Quot)Quot                              !d1
	(2000,(?pitch,1.5)Quot)Quot                              !d2
	(3000,(?pitch,1.5)Quot)Quot                              !d3
	(1000,(?cPitch,1.5)Quot)Quot                             !cd1
	(2000,(?cPitch,1.5)Quot)Quot                             !cd2
	(3000,(?cPitch,1.5)Quot)Quot                             !cd3
	
	[
	 Circular and radial coupled resonance hand tuned for this drum 
	]
	(
		?sample,
		(		
			((?shape,-5 )SF.Volume ,?d1 ),
			((?shape,-10 )SF.Volume,?d2 ),
			((?shape,-10)SF.Volume ,?d3 ),
			((?shape,-3)SF.Volume  ,?cd1),
			((?shape,-8)SF.Volume  ,?cd2),
			((?shape,-8)SF.Volume  ,?cd3)
		)
	)SF.Resonate SF.Normalise                                !sample
	
	[ Pick the mix of resonant and noise singal ]
	(>sample,?drive SF.Invert )SF.Add SF.Normalise           !resonant

	(?resonant,(?pitch,4)Prod,4)SFFilter.NHighPass           !toExcite
	>toExcite SFAMP.WarmALittle                              !excited
	(>excited,?clatter)SF.Volume                             !excited
	(>excited,>resonant)SF.Add SF.Normalise                  !resonant
	
	(
		(>resonant,(?mix,100)Quot)SF.NumericVolume,
		(>drive,((100,?mix)Diff,100)Quot)SF.NumericVolume
	)SF.Add                                                  !sample
	
	[ Remove DC ]
	((?sample,1,1)SFFilter.NHighPass,3.0)SF.Volume           !sampleB
	(>sample SF.Invert,>sampleB)SF.Add SF.Normalise          !sample

	(?sample,(?pitch,4)Prod,4)SFFilter.NHighPass             !toExcite
	>toExcite SFAMP.WarmALittle                              !excited
	(>excited,?clatter)SF.Volume                             !excited
	(>excited,>sample)SF.Add SF.Normalise                    !sample
	((0,1),((?len,10)Diff,1),(?len,0))SFShape.Numeric        !shape
	(>sample,>shape)SF.Multiply                              !sample

	"Done Drum" PrintLn   
	>sample
} _bass-drum

 1600   !pitch
  100   !len
[ 
  0  -> Mellow
  3  -> Normal
  6  -> Slap that drum up
  9  -> Loosing it on stage
  18 -> Hitting a harder or much less resonant drum
]
3    !clatter
List !drumDo
(
	0,10,
	{
		!mix
		((>mix,5)Prod,35)Sum              !mix
		(_bass-drum Do,>drumDo)ListAddEnd !drumDo
	}
)Repeat

(
	0,10,
	{
		!mix
		(((10,>mix)Diff,5)Prod,35)Sum       !mix
		(_bass-drum Do ,>drumDo)ListAddEnd !drumDo
	}
)Repeat

List !drum
(
	0,21,
	{
		(?drumDo ListHead Done, ?drum)ListAddEnd !drum
		>drumDo  ListTail !drumDo 
	}
)Repeat
>drum SF.Concatenate SF.ClipToSafe SF.Normalise  !sample

_play Do Done