[ Read the input ]
"temp/input.wav"           !wavFile
?wavFile ReadFile GetStart Normalise !signal
[(
    ?signal,
    ?signal,
    ?signal,
    ?signal
)Concatenate !signal]
(?signal,0.2)DirectRelength !signalL
(>signal,0.2)DirectRelength !signalR
32 !pitchL
33 !pitchR
(
    (?signalL Length,((3,0.5)**,0.5)*)SinWave
    (?signalL Length,((2,0.5)**,0.5)*)SinWave
)Mix Normalise !modL
(
    (?signalL Length,((3.1,0.5)**,0.5)*)SinWave
    (?signalR Length,((1.9,0.5)**,0.5)*)SinWave
)Mix Normalise !modR
(1,>modL pcnt+10)DirectMix !modL
(1,>modR pcnt+10)DirectMix !modR
(>signalL,0.95,0.10,?pitchL Period,>modL)ShapedResonantFilter Normalise !left
(>signalR,0.95,0.10,?pitchR Period,>modR)ShapedResonantFilter Normalise !right
(>left, 0.25)DirectRelength !left
(>right,0.25)DirectRelength !right

(?left,0.95,0.10, 75 Period )ResonantFilter Normalise !leftLow
(?right,0.95,0.10,75 Period)ResonantFilter  Normalise !rightLow
(
    (>leftLow,110,6)DirectLowPass Normalise dbs+12 Saturate Normalise,
    >left  Normalise pcnt+50
)Mix !left
(
    (>rightLow,110,6)DirectLowPass Normalise dbs+12 Saturate Normalise,
    >right Normalise pcnt+50
)Mix !right
(>left, 1000,1)DirectLowPass Normalise !left
(>right,1000,1)DirectLowPass Normalise !right
(>left, 25,2)DirectHighPass  Normalise !left
(>right,25,2)DirectHighPass  Normalise !right
{
    100 Prime !pitch
    (
        ?signal,
        (
            (-16.0,(1200 Prime,?pitch)/),
            (-15.0,(1500 Prime,?pitch)/),
            (-15.0,(1800 Prime,?pitch)/),
            (-15.0,(2000 Prime,?pitch)/),
            (-14.0,(4000 Prime,?pitch)/)
        )
    ) DirectResonate  Normalise Saturate Normalise !signal
    1000 Prime !pitch
    (
        ?signal,
        (
            (-16.0,(1200 Prime,?pitch)/),
            (-15.0,(1500 Prime,?pitch)/),
            (-15.0,(1800 Prime,?pitch)/),
            (-15.0,(2000 Prime,?pitch)/),
            (-14.0,(4000 Prime,?pitch)/)
        )
    ) DirectResonate  Normalise Saturate Normalise !signal
    (?signal Rectify,50,2)DirectLowPass !shape 
    (1.0,(>shape,0.5)Power dbs+3)DirectMix      !shape
    (
        >signal,
        >shape 
    )Divide Normalise !signal
} !post
>left !signal  ?post Do !left  >signal 
>right !signal ?post Do !right >signal 
(>left Done,>right Done)   !bus
(?bus,"temp/output-done.wav")WriteFile32
