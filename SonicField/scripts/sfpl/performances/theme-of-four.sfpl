[Play a sample]
{
	((?sample),"c:\temp\tone.wav")WriteFile16
	("c:\temp\tone.wav","Primary Sound Driver" Mixer)PlayFile LineWait
} _play
{
	[ Parameters:
	  len     - length of note.
	  mix     - percent of resonant signal
	  pitch   - the pitch around which the drum is based 
	  clatter - high pitch boost
	]
	
	"Starting Drum" PrintLn
	[
	 hand tuned noise envelope to make the drive for the drum
	]   
	(?len WhiteNoise)Concatenate                       !sample
	(
		((0,1),((?len,0.5)*,-16),(?len,-36))SimpleShape,
		>sample
	)Multiply                                             !sample
	
	[
	 The input noise is split into bands to agitate the radial and
	 circular resonance.
	]
	(
		?sample,
		(?pitch,1.5)/,
		(?pitch,0.75)/,
		2
	)DirectBandPass Normalise                          !rSample
	(?pitch,PI)/                                     !cPitch
	(
		?sample,
		(?cPitch,1.5)/,
		(?cPitch,0.75)/,
		2
	)DirectBandPass Normalise                          !cSample

	(
		(>rSample, 0.0)Volume,
		(>cSample, 0.0)Volume
	)Mix Normalise                                     !sample
	(>sample,(?pitch,6)*,1)DirectLowPass              !drive    !sample
	
	((0,0),(5,1),(10,0))NumericShape                      !shape
	 [
	  Black magic!
	  ============
	  
	  The setting for the shape of the convolution is simply
	  hand tunned to make the drum sound nice.
	 ]
	(>shape,(?pitch,150)/)DirectResample         !shape
	>shape NormaliseArea                                  !shape
	[ This controls the gain of the resonator, which can be
	  effected by the sample rate ]
	(>shape,1.6)Volume                                 !shape
	(1000,(?pitch,1.5)/)/                              !d1
	(2000,(?pitch,1.5)/)/                              !d2
	(3000,(?pitch,1.5)/)/                              !d3
	(1000,(?cPitch,1.5)/)/                             !cd1
	(2000,(?cPitch,1.5)/)/                             !cd2
	(3000,(?cPitch,1.5)/)/                             !cd3
	
	[
	 Circular and radial coupled resonance hand tuned for this drum 
	]
	(
		?sample,
		(		
			((?shape,-5 )Volume ,?d1 ),
			((?shape,-10 )Volume,?d2 ),
			((?shape,-10)Volume ,?d3 ),
			((?shape,-3)Volume  ,?cd1),
			((?shape,-8)Volume  ,?cd2),
			((?shape,-8)Volume  ,?cd3)
		)
	)Resonate Normalise                             !sample
	
	[ Pick the mix of resonant and noise singal ]
	(>sample,?drive Invert )Mix Normalise           !resonant

	(?resonant,(?pitch,4)*,4)DirectHighPass         !toExcite
	>toExcite WarmALittle                           !excited
	(>excited,?clatter)Volume                       !excited
	(>excited,>resonant)Mix Normalise               !resonant
	
	(
		(>resonant,(?mix,100)/)NumericVolume,
		(>drive,((100,?mix)-,100)/)NumericVolume
	)Mix                                            !sample
	
	[ Remove DC ]
	((?sample,1,1)DirectHighPass,3.0)Volume         !sampleB
	(>sample Invert,>sampleB)Mix Normalise          !sample

	(?sample,(?pitch,4)*,4)DirectHighPass           !toExcite
	>toExcite WarmALittle                           !excited
	(>excited,?clatter)Volume                       !excited
	(>excited,>sample)Mix Normalise                 !sample
	((0,1),((?len,10)-,1),(?len,0))NumericShape     !shape
	(>sample,>shape)Multiply                        !sample

	"Done Drum" PrintLn   
	(>sample,?volume)Volume
} _play-drum

[ Parameters:
  len     - length of note.
  mix     - percent of resonant signal
  pitch   - the pitch around which the drum is based 
  clatter - high pitch boost
  volume  - volume dbs +-
]

[ 
  0  -> Mellow
  3  -> Normal
  6  -> Slap that drum up
  9  -> Loosing it on stage
  18 -> Hitting a harder or much less resonant drum
]
    0   !clatter
  500   !pitch-base
  100   !len-base
  
0    !plays-a
List !drum-do-a
{
	(
		1,8,
		{
			3                                 !volume
			25                                !mix
			6                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
			
			0                                 !volume
			25                                !mix
			0                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
	
			0                                 !volume
			75                                !mix
			0                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
	
			0                                 !volume
			25                                !mix
			0                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
			
		}
	)Repeat
} _rythum-a

{
	(
		1,8,
		{
			3                                 !volume
			25                                !mix
			6                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
			
			(
				1,2,
				{
					0                                 !volume
					25                                !mix
					0                                 !clatter
					(?len-base,2)/                    !len
					?pitch-base                       !pitch
					(
						_play-drum Do,
						>drum-do-a
					)ListAddEnd                       !drum-do-a
					(>plays-a,1)+                     !plays-a
				}
			)Repeat
	
			0                                 !volume
			75                                !mix
			0                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
	
			0                                 !volume
			25                                !mix
			6                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
			
		}
	)Repeat
} _rythum-b

{
	(
		1,4,
		{
			3                                 !volume
			50                                !mix
			6                                 !clatter
			(?len-base,2)*                    !len
			(?pitch-base,1.2)*                !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a

			3                                 !volume
			75                                !mix
			3                                 !clatter
			(?len-base,2)*                    !len
			(?pitch-base,1.1)*               !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
			
			3                                 !volume
			25                                !mix
			6                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
			
			(
				1,2,
				{
					0                                 !volume
					25                                !mix
					0                                 !clatter
					(?len-base,2)/                    !len
					?pitch-base                       !pitch
					(
						_play-drum Do,
						>drum-do-a
					)ListAddEnd                       !drum-do-a
					(>plays-a,1)+                     !plays-a
				}
			)Repeat
	
			0                                 !volume
			75                                !mix
			0                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
	
			0                                 !volume
			25                                !mix
			6                                 !clatter
			?len-base                         !len
			?pitch-base                       !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
			
		}
	)Repeat
} _rythum-c

{
	(
		1,4,
		{
			3                                 !volume
			20                                !mix
			0                                 !clatter
			(?len-base,2)*                    !len
			(?pitch-base,1.2)*                !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a

			3                                 !volume
			50                                !mix
			0                                 !clatter
			(?len-base,2)*                    !len
			(?pitch-base,1.1)*               !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a

			0                                 !volume
			80                                !mix
			3                                 !clatter
			(?len-base,2)*                    !len
			(?pitch-base,1.2)/                !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a

			0                                 !volume
			80                                !mix
			3                                 !clatter
			(?len-base,2)*                    !len
			(?pitch-base,1.1)/               !pitch
			(
				_play-drum Do,
				>drum-do-a
			)ListAddEnd                       !drum-do-a
			(>plays-a,1)+                     !plays-a
	
		}
	)Repeat
} _rythum-d

0    !plays-b
List !drum-do-b
(
	0,71,
	{
		6                                 !volume
		75                                !mix
		3                                 !clatter
		(?len-base,8)*                    !len
		(?pitch-base,6)/                  !pitch
		(
			_play-drum Do,
			>drum-do-b
		)ListAddEnd                       !drum-do-b
		(>plays-b,1)+                     !plays-b
	}
)Repeat

6                                 !volume
50                                !mix
6                                 !clatter
(?len-base,8)*                    !len
(?pitch-base,6)/                  !pitch
(
	_play-drum Do,
	>drum-do-b
)ListAddEnd                       !drum-do-b
(>plays-b,1)+                     !plays-b

_rythum-a Invoke
_rythum-b Invoke
_rythum-a Invoke
_rythum-b Invoke
_rythum-a Invoke
_rythum-c Invoke
_rythum-b Invoke
_rythum-c Invoke
_rythum-a Invoke

_rythum-d Invoke
_rythum-b Invoke
_rythum-d Invoke
_rythum-b Invoke
_rythum-c Invoke
_rythum-c Invoke
_rythum-b Invoke
_rythum-a Invoke

List !samples
( 
   1,>plays-a,
    {
    	?drum-do-a ListHead !job
    	>drum-do-a ListTail !drum-do-a
   		?job Done           !sample
   		(>sample,>samples)ListAddEnd !samples
    }
)Repeat

>samples Concatenate ClipToSafe Normalise    !drums-a

List !samples
( 
   1,>plays-b,
    {
    	?drum-do-b ListHead !job
    	>drum-do-b ListTail !drum-do-b
   		?job Done           !sample
   		(>sample,>samples)ListAddEnd !samples
    }
)Repeat

>samples Concatenate ClipToSafe Normalise    !drums-b
(
	(?len-base,16)* Silence,
	>drums-a
)Concatenate                !drums-a

(>drums-a,>drums-b)Mix Normalise             !sample

_play Do Done