[Play a sample]
{
	((>left,>right),"c:\temp\tone.wav")WriteFile16
	("c:\temp\tone.wav","Primary Sound Driver" Mixer)PlayFile LineWait
} _play

[ Read the input ]
"C:\temp\tone-in.wav"  !wavFile
?wavFile ReadFile            !channels
?channels ListHead              !sample

?sample Rectify                !signal
(>signal,10,2)DirectLowPass    !signal
(?signal,0.25)Power            !signal
(>signal,?sample)Multiply      !distort
?distort Normalise             !distort
>distort WarmALittle           !distort
?sample Normalise              !sample
(>distort,0)Volume             !distort
(>distort,2000,2)DirectLowPass !distort
(>distort,>sample)Mix          !sample
>sample Normalise              !sample

[ An Exciter Effect ]
{
	(?sample,2000,1)DirectLowPass                               !a
	((?a !a,2000,4)DirectHighPass,0.9)Power Normalise     !b
	(?a,(?b,-8)Volume)Mix Normalise
} _exciter

[ An effect which adds some bass ]
{ 
	(((?sample,220,4)DirectLowPass,?sample)Mix,-3)Volume
} _speachBassBoost

[ An effect which adds a delay ad attenuates the result ]
{
	((?delayTime Silence,?sample) Concatenate,?wetDbs)Volume 
} _simpleDelay

[ An effect to mix two samples and normalize the result ]
{ 
	(?sampleA,?sampleB)Mix Normalise 
} _simpleMix

_speachBassBoost Do !leftBassTask

?leftBassTask    Done !left
?left                 !right

(>left,0.95) Power !sample !left
(>right,0.95)Power !sample !right
[>left  Done      !left
>right Done       !right
]
20   !delayTime
-3   !wetDbs
?left           !sample
_simpleDelay Do !leftDelayTask

25   !delayTime
?right          !sample
_simpleDelay Do !RightDelayTask


?left                !sampleA
?leftDelayTask Done  !sampleB
_simpleMix Do        !leftMixTask

?Right               !sampleA
?RightDelayTask Done !sampleB
_simpleMix Do        !rightMixTask

?leftMixTask  Done !left
?RightMixTask Done !Right

[ Make This And Effect !]
(
	1,6,
	{
	    !t
	    {((30,(?t,10)*)+ Silence,?left)Concatenate } Do !nlTask
	    {((40,(?t,10)*)+ Silence,?right)Concatenate} Do !nrTask
	
	    {((?nlTask Done, -18)Volume,1000,2)DirectLowPass} Do  !nlTask
	    {((?nrTask Done, -18)Volume,1000,2)DirectLowPass} Do  !nrTask
	
	    {(?nlTask Done, ?left) Mix Normalise} Do !nlTask 
	    {(?nrTask Done, ?right)Mix Normalise} Do !nrTask
		
		?nlTask Done !left
		?nrTask Done !right
	}
)Repeat

_play Do Done