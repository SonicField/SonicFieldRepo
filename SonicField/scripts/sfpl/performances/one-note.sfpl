96000 SetSampleRate
{
    Bunch !signals
    ?pitch !o-pitch !pitch-end
    1      !cut
    {
        {
            ((0,?pitch),((?length,2)*,?pitch-end))RestrictedSlide Invert !signal
            (>signal,(1,?cut)/)NumericVolume !signal
            bunch !sigs
            (
                1,5,
                {
                    (
                        (((?length,2)/,Random)*,)+,
                        (((?length,2)/,Random)*,)+
                    )+                                    !pos
                    ((64,Random)*,(64,Random)*)+          !freq
                    (?o-pitch,?freq Truncate)*            !freq
                    (1000,(5000,Random)*)+                !len
                    (
                        (0,0),
                        (256,Random),
                        ((?len,256)-,Random),
                        (?len,0)
                    )NumericShape                         !env
                    (
                        (?len,?freq)SinWave,
                        ?env
                    )Multiply                             !high
                    ((>high,>pos),>sigs)AddEnd            !sigs
                }
            )Repeat
            ((>signal,0),>sigs)AddEnd                     !sigs
            >sigs MixAt !signal
            
        }Do !signal
        (>pitch     ,?o-pitch)+       !pitch
        (>pitch-end ,?o-pitch)+       !pitch-end
        (>pitch-end,1.00)*            !pitch-end
        (>cut,2)+                 !cut
        (?signal,>signals)AddEnd  !signals
        ?pitch Println
        (?pitch,320)lt        
    }
    loop
    ?signals Mix Normalise Saturate !signal
}!alias-free-saw
180000 !length

   32.01           !pitch
?alias-free-saw Do !sigl
   31.99           !pitch
?alias-free-saw Do !sigr
   32.1            !pitch
?alias-free-saw Do !siglr
   31.9            !pitch
?alias-free-saw Do !sigrl

(>sigl, "temp/sigl")WriteSignal
(>sigr, "temp/sigr")WriteSignal
(>siglr,"temp/siglr")WriteSignal
(>sigrl,"temp/sigrl")WriteSignal

"temp/sigl" ReadSignal  !sigl
"temp/sigr" ReadSignal  !sigr
"temp/siglr" ReadSignal !siglr
"temp/sigrl" ReadSignal !sigrl

{
    (
        {(>sigl,0)}Do,
        {(>siglr pcnt-75,10)}Do,
        {(>sigrl pcnt-25,30)}Do
    )MixAt
}Do !sigl

{
    (
        {(>sigr,0)}Do,
        {(>sigrl pcnt-75,10)}Do,
        {(>siglr pcnt-25,30)}Do
    )MixAt
}Do !sigr

>sigrl Final
>siglr Final
((?sigl,?sigr),"temp/y.wav")WriteFile32

{
    (
        0,
        0,
        0,
        0.5,
        1,
        10,
        >signal
    ) WaveShaper Normalise !signal
    [(>signal,2)NumericVolume Saturate Normalise]
}!post

>sigl !signal ?post Do !sigl 
>sigr !signal ?post Do !sigr
((?sigl,?sigr)Mix,128,2)ButterworthLowPass !low

{
    (
        >sigl pcnt+90,
        >low  pcnt+10
    )Mix Normalise 
}Do !left
>sigl Final
{
    (
        >sigr pcnt+90,
        >low  pcnt+10
    )Mix Normalise 
}Do !right
>sigr Final

"temp/terrys_warehouse_stereo_short.wav" ReadFile ^convoll ^convolr
>convoll Normalise !convoll
>convolr Normalise !convolr

{?left  !signal >convoll !convol ?reverb Do Normalise}Do !wleft
{?right !signal >convolr !convol ?reverb Do Normalise}Do !wright

{
    (
        >wleft pcnt+50,
        >left  pcnt+50
    )Mix Normalise
}Do !left
{
    (
        >wright pcnt+50,
        >right  pcnt+50
    )Mix Normalise
}Do !right

[
   Quite bright to start with - be careful 
]

((>left,>right),"temp/x.wav")WriteFile32