"temp/input.wav" ReadFile ^left ^right

{
    ("Granulate")Println
    Bunch !out
    (
        (>signal,15000,25)Granulate,
        {
            ^grain ^time
            ?grain check
            (?time,(Random,60)*)+       !time
            ((Random,0.75)*,0.25)+      !vol
            (>grain,>vol)NumericVolume  !grain
            ((>grain,>time),>out)AddEnd !out
        }
    )InvokeAll
    >out MixAt Normalise
}!haas

{
    ("Filtering")Println
    (>signal ,?lower, 2)BesselHighPass !signal
    (>signal ,?upper, 2)BesselLowPass  !signal
    ?haas Do
}!band-widen

{
    >signal Normalise !signal
    (
        (
            -0.03,0.2,0,-1,0.2,2,
            ?signal
        )WaveShaper pcnt+75,
        >signal pcnt+25
    )Mix Normalise !signal
    
    Bunch !sigs
    (
      1,14,
      {
        !band
        (?band,2)**           !bandl 
        ((>band,1)+,2)**      !bandh 
        ((?bandl,100)*,75)-   !lower
        ((?bandh,100)*,75)-   !upper
        ?band-widen Do        !job
        (>job,>sigs)AddEnd    !sigs
      }
    )Repeat

    [ Final over all compress ]
    >sigs Mix Normalise dbs+3 wavelimit !signal
}!do-it 

{
    Bunch !grains
    (
        ?blocks,
        {
            ^signal ^time
            ("Block ",?time)Println
            ?do-it Do Done ToGrain !grain
            ((>grain,>time),>grains)AddEnd !grains
        }
    )InvokeAll
    "Joing blocks" Println
    >grains MixAt Normalise !ret
    "Blocks joined" Println
    >ret
}!do-blocks

(>left ,200000)Blockulate  !blocks
?do-blocks Do !left
>blocks
(>right,200000)Blockulate !blocks
?do-blocks Do !right
(>left,>right)!out
"About to write" println
(>out,"temp/done.wav")WriteFile32
