"scripts/play/stylophone-2.wav" ReadFile ^signal
(?signal,50,50)Follow       !shape
(>shape Reverse,150,50)Follow !shape
>shape Reverse                !shape
(?shape,(1,?shape MaxValue)/)NumericVolume !shape
(>signal,?shape)Multiply Normalise         !signal
(?signal,(500,(?shape,1600)NumericVolume)DirectMix,((0,0.5),(?shape length,0.5))NumericShape)ShapedLadderLowPass Normalise !signal
(?signal,(250,(?shape, 800)NumericVolume)DirectMix,?shape)ShapedLadderLowPass Normalise !signalb
(>signal,>signalb)Mix Normalise !signal
((?signal,500,2)RBJLowPass,50)Granulate !grains
0.50 !slower
Bunch !low
(
    ?grains,
    {
       ^grain ^time
       (>grain,?slower)DirectRelength !grain
       ((>grain,>time),>low)AddEnd !low
    }
)InvokeAll
Bunch !medium
0.75 !slower
(
    ?grains,
    {
       ^grain ^time
       (>grain,?slower)DirectRelength !grain
       ((>grain,>time),>medium)AddEnd !medium
    }
)InvokeAll
(
    >low MixAt,
    >medium MixAt,
    >signal 
)Mix !signal
{
    (?sample,1000,5)ButterworthLowPass  SwapOut !sampleL
    (?sample,3000,2)ButterworthLowPass  SwapOut !sampleN
    (?sample,1000,4)ButterworthHighPass SwapOut !sampleH
    (>sampleH SwapIn,5000,2)ButterworthLowPass SwapOut !sampleH
    (?sample,((?first ,?offset)+ Prime Silence,(?sample,-13)Volume)Concatenate)Mix  SwapOut  !wet
    (
        1,2,
        {
            !z
            (>first,1.2)* Prime !first
            (>wet SwapIn,(((?first,30)-,?offset)- Silence,(?sampleL ReadIn,(-10,?z)-)Volume)Concatenate)Mix  SwapOut  !wet
            (>wet SwapIn,(((?first,30)+,?offset)- Silence,(?sampleN ReadIn,(-6, ?z)-)Volume)Concatenate)Mix  SwapOut  !wet
            (>wet Swapin,((?first      ,?offset)- Silence,(?sampleH ReadIn,(-10,?z)-)Volume)Concatenate)Mix  SwapOut  !wet
        }
    )Repeat
    (?sampleL SwapIn,1000,1)ButterworthLowPass SwapOut !sampleL
    (?sampleN SwapIn,1000,1)ButterworthLowPass SwapOut !sampleN
    (?sampleH SwapIn,2000,1)ButterworthLowPass SwapOut !sampleH
    (
        (0,0),
        ((?wet ReadIn Length,3)/,1),
        (?wet  ReadIn Length,0)
    )NumericShape !env
    (
        3,20,
        {
            !z 
            (?z,3)/ !z
            (>first,1.1)* Prime !first
            (>wet SwapIn,(((?first,30)-,?offset)- Silence,((?sampleL ReadIn,(-7,?z)-)Volume,?env)Multiply)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,(((?first,30)+,?offset)- Silence,((?sampleN ReadIn,(-4,?z)-)Volume,?env)Multiply)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,((?first      ,?offset)- Silence,((?sampleH ReadIn,(-7,?z)-)Volume,?env)Multiply)Concatenate)Mix SwapOut !wet
        }
    )Repeat
    (
        20,40,
        {
            !z 
            (?z,4)/ !z
            (>first,1.05)* Prime !first
            (>wet SwapIn,(((?first,30)-,?offset)- Silence,(((?sampleL ReadIn,(-7,?z)-)Volume,?env)Multiply,500,2)ButterworthLowPass)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,(((?first,30)+,?offset)- Silence,(((?sampleN ReadIn,(-4,?z)-)Volume,?env)Multiply,500,2)ButterworthLowPass)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,((?first      ,?offset)- Silence,(((?sampleH ReadIn,(-7,?z)-)Volume,?env)Multiply,500,2)ButterworthLowPass)Concatenate)Mix SwapOut !wet
        }
    )Repeat
    >wet SwapIn
} !reflector

80 !first
30 !offset
?signal !sample
?reflector Do !left
?signal !sample
?reflector Do !right

(
    (
        >left  Done Normalise  Normalise,
        >right Done Normalise Normalise
    )StereoMonitor,"temp/tone.wav"
)WriteFile32
