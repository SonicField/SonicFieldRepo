{
    ("Scanning channel ",?count,?pitch)Println
    ( >signal       ,?pitch,0.1,48)RBJPeaking            !chan-sig
    (?chan-sig Final,?pitch,0.1,48)RBJPeaking            !chan-sig 
    (>chan-sig Final,?pitch Period,?pitch Period)Follow  !chan-sig 
    (>chan-sig Final,?pitch,2)ButterworthLowPass 
}!scan


{
    Bunch !out
     1 Silence !output
    32 !pitch
    (
        1,?bands,
        {
            !count
            {
                {
                    (?scan Do Final,(1,?stretch)/)DirectRelength !chan-sig        
                    ("Encoding channel ",?count,?pitch)Println      
                    >chan-sig
                }Do !chan-sig
                                  
                {
                    (?length,?stretch)* WhiteNoise          !signal
                    (?signal Final,?pitch,1.0,48)RBJPeaking !signal
                    (?signal Final,?pitch,0.1,48)RBJPeaking !signal
                    (?signal Final,?pitch,?pitch,1)ButterworthBandPass !signal
                    >signal Final Normalise 
                }Do !signal
                
                (>signal Final,?chan-sig Final)Multiply !signal
                ?signal Magnitude !m1
                (>signal Final,2)Power !signal
                ?signal Magnitude !m2
                (>signal Final,(?m1,?m2)/)NumericVolume
            }Do !x
            (>x,>out)AddEnd !out
            (>pitch,?step)* !pitch
        }
    )Repeat
    >out Mix Normalise
} !vocode 

"temp/input.wav" ReadFile ^left ^right
(5000,125000,>left )Cut !left
(5000,125000,>right)Cut !right

?left length !length
 5            !stretch
 4            !resolution
(
    Semitone,
    (1,?resolution)/
)**                !step
(108,?resolution)* !bands

   32  !pitch
"left" !side
>left  !signal
(?vocode Do Final,"/Volumes/Tichy/temp/left")WriteSignal

     32 !pitch
"right" !side
>right  !signal
(?vocode Do Final,"/Volumes/Tichy/temp/right")WriteSignal

(
    (
        "/Volumes/Tichy/temp/left"  ReadSignal,
        "/Volumes/Tichy/temp/right" ReadSignal
    ),"temp/tone.wav"
)WriteFile32