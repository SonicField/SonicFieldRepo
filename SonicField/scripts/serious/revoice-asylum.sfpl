"temp/input.wav" ReadFile ^signal
?signal !orig

[Low peak cut]
(?signal,150,3)ButterworthLowPass  !lower
(?signal,150,3)ButterworthHighPass !upper
(>lower,60,2)ButterworthHighPass   !lower
(?lower Normalise,100,100)Follow           !shape
(?shape,(1,?shape MaxValue)/)NumericVolume !shape
(0.1,>shape dbs+48)DirectMix               !shape
(
    >lower,
    >shape 
)Divide !lower

(
    >lower,
    >upper 
)Mix  Normalise !signal

[ De-ess ]
(?signal,4000,5)ButterworthLowPass  !lower
(?signal,4000,5)ButterworthHighPass !upper
(?upper Normalise,100,100)Follow           !shape
(?shape,(1,?shape MaxValue)/)NumericVolume !shape
(0.1,>shape dbs+48)DirectMix               !shape
(
    >upper,
    >shape 
)Divide !upper

(
    (
        (
            >lower,
            >upper dbs+3
        )Mix  Normalise ,1000,0.25
    )RBJLowPass,
    350,
    0.2
)RBJHighPass Normalise dbs+24 Saturate dbs+6 clip Normalise !signal 

(>signal,1000,1)BesselHighPass Normalise !signal 

(
    (?signal Reverse,1,50)Follow Reverse,
    (?signal        ,1,50)Follow,
)Mix !shape    
(?shape,(2,?shape MaxValue)/)NumericVolume   !shape
(0.04,>shape)DirectMix Clip                  !shape
(>signal,?shape)Multiply Normalise           !signal

(
    (?signal Normalise,25,25)Follow,
    (?signal Normalise Reverse,25,25)Follow Reverse,
)Mix !shape
(?shape,(1,?shape MaxValue)/)NumericVolume !shape
(0.3,>shape dbs+24)DirectMix               !shape
(
    >signal,
    >shape 
)Divide Normalise !signal !clean

(
    (
        ((?signal length WhiteNoise,500,2)RBJLowPass,12,1)RBJHighPass Normalise,
        ((2,(?signal length,1)SinWave)DirectMix,0.25)NumericVolume
    )Multiply Normalise pcnt+25,
    >signal pcnt+75
)Mix !signal

(
    (
        (((?signal length WhiteNoise,6)Power Normalise,6)Power Normalise,6)Power Normalise,
        ((2,(?signal length,1)SinWave)DirectMix,0.25)NumericVolume
    )Multiply Normalise pcnt+25,
    >signal pcnt+75
)Mix !signal

(
    (((0,0),(?signal length,1))NumericShape,>signal)Multiply Normalise,
    (((0,1),(?clean  length,0))NumericShape,>clean )Multiply Normalise
)Mix !signal

(?signal length,0.5)SinWave !wobble
(>signal,50,50)Granulate !grains
Bunch !signal
0.925 !init-slow
?init-slow !slower
(
    ?grains,
    {
       ^grain ^time
       (>grain,?slower)DirectRelength  !grain
       ((>grain,?time),>signal)AddEnd  !signal
       (((?wobble,?time)ValueAt,0.025)*,?init-slow)+ !slower 
    }
)InvokeAll
>signal MixAt Normalise !clean  

(?clean,500)Granulate !grains
Bunch !dirty
(
    ?grains,
    {
       ^grain ^time
       (?time,(0.995,(0.01,Random)*)+)* !time1
       (?time,(0.995,(0.01,Random)*)+)* !time2
       ((?grain,?time1),>dirty)AddEnd  !dirty
       ((>grain,?time2),>dirty)AddEnd  !dirty
    }
)InvokeAll
>dirty MixAt Normalise !dirty 
(
    (((0,0),((?dirty length,0.75)*,0),(?dirty length,1))NumericShape,>dirty)Multiply Normalise,
    (((0,1),((?clean length,0.75)*,1),(?clean length,0))NumericShape,>clean)Multiply Normalise
)Mix !signal 

[ Reverb ]
(15 Silence,?signal)Concatenate !note-left
?signal                         !note-right

(>note-left ,1000,0.25)RBJLowPass !note-left
(>note-right,1000,0.25)RBJLowPass !note-right
(
    1,2,
    {    
        !outer
        (
            1,4,
            {
                !t
                {((30,(?t,(24,?outer)*)*)+ Prime Silence,?note-left )Concatenate} Do !nlTask
                {((45,(?t,(15,?outer)*)*)+ Prime Silence,?note-right)Concatenate} Do !nrTask
            
                {((>nlTask Done, -15)Volume,(700,(?t,3)*)-,1)ButterworthLowPass}  Do  !nlTask
                {((>nrTask Done, -15)Volume,(700,(?t,3)*)-,1)ButterworthLowPass}  Do  !nrTask
            
                {(>nlTask Done, ?note-left )Mix Normalise}                        Do !nlTask 
                {(>nrTask Done, ?note-right)Mix Normalise}                        Do !nrTask
                
                ?nlTask Done !note-left
                ?nrTask Done !note-right
            }
        )Repeat
    }
)Repeat
(
    >note-left dbs-20,
    ?signal
)Mix Normalise !left
(
    >note-right dbs-20,
    >signal
)Mix Normalise !right

(>left ,3000,0.3)RBJLowPass  Normalise  !left
(>right,3000,0.3)RBJLowPass  Normalise  !right
(>left, 150, 0.3)RBJHighPass Normalise  !left
(>right,150, 0.3)RBJHighPass Normalise  !right

(
    ( 
        >left,
        >right
    ),"temp/tone.wav"
)WriteFile32 
