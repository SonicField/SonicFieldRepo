"temp/input.wav" ReadFile ^left ^right
{
    Bunch !out
    (
        (>signal,2500,25)Granulate,
        {
            ^grain ^time
            (?time,(Random,60)*)+ !time
            ((>grain,>time),>out)AddEnd !out
        }
    )InvokeAll
    >out MixAt Normalise
}!haas

{
    (
        (
            -0.03,0.2,0,-1,0.2,2,
            ?signal
        )WaveShaper pcnt+25,
        >signal pcnt+75
    )Mix Normalise !signal
    (
        {(?signal Reverse,1,50)Follow Reverse},
        {(?signal        ,1,50)Follow}
    )DoAll Mix !shape   
    
    (
        {(?signal Normalise,25,25)Follow},
        {(?signal Normalise Reverse,25,25)Follow Reverse}
    )DoAll Mix !shape
    
    (?shape,(1,?shape MaxValue)/)NumericVolume !shape
    (0.05,>shape dbs+6)DirectMix               !shape
    (
        ?signal,
        >shape 
    )Divide Normalise !signal1
    (
        >signal,
        >signal1
    )Mix Normalise !signal
         
}!envelope-compress

{
    (>signal,0.01)MaxDeviation Normalise !signal
    (?signal ,200,2000,4)BesselBandPass !signal-m 
    (?signal     ,2000,4)BesselHighPass !signal-h 
    (?signal     , 200,4)BesselLowPass  !signal-l 
    >signal-m !signal
    ?envelope-compress Invoke
    ?haas Do !signal-m
    >signal-h !signal
    ?envelope-compress Invoke
    ?haas Do !signal-h
    >signal-l !signal
    ?envelope-compress Invoke
    ?haas Do !signal-l
    (
        (>signal-l ,200,6)BesselLowPass,
        (>signal-m ,2000,6)BesselLowPass,
        (>signal-h ,2000,6)BesselHighPass
    )Mix Normalise 
}!do-it 
>left  !signal
?do-it Do !left
>right !signal
?do-it Do !right

((>left Done,>right Done),"temp/done.wav")WriteFile32
