{
    (?sample,1000,5)ButterworthLowPass  SwapOut !sampleL
    (?sample,3000,2)ButterworthLowPass  SwapOut !sampleN
    (?sample,1000,4)ButterworthHighPass SwapOut !sampleH
    (>sampleH SwapIn,5000,2)ButterworthLowPass SwapOut !sampleH
    (?sample,((?first ,?offset)+ Prime Silence,(?sample,-13)Volume)Concatenate)Mix  SwapOut  !wet
    (
        1,2,
        {
            !z
            (>first,1.2)* Prime !first
            (>wet SwapIn,(((?first,30)-,?offset)- Silence,(?sampleL ReadIn,(-10,?z)-)Volume)Concatenate)Mix  SwapOut  !wet
            (>wet SwapIn,(((?first,30)+,?offset)- Silence,(?sampleN ReadIn,(-6, ?z)-)Volume)Concatenate)Mix  SwapOut  !wet
            (>wet Swapin,((?first      ,?offset)- Silence,(?sampleH ReadIn,(-10,?z)-)Volume)Concatenate)Mix  SwapOut  !wet
        }
    )Repeat
    (?sampleL SwapIn,1000,1)ButterworthLowPass SwapOut !sampleL
    (?sampleN SwapIn,1000,1)ButterworthLowPass SwapOut !sampleN
    (?sampleH SwapIn,2000,1)ButterworthLowPass SwapOut !sampleH
    (
        (0,0),
        ((?wet ReadIn Length,3)/,1),
        (?wet  ReadIn Length,0)
    )NumericShape !env
    (
        3,20,
        {
            !z 
            (?z,4)/ !z
            (>first,1.1)* Prime !first
            (>wet SwapIn,(((?first,30)-,?offset)- Silence,((?sampleL ReadIn,(-7,?z)-)Volume,?env)Multiply)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,(((?first,30)+,?offset)- Silence,((?sampleN ReadIn,(-4,?z)-)Volume,?env)Multiply)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,((?first      ,?offset)- Silence,((?sampleH ReadIn,(-7,?z)-)Volume,?env)Multiply)Concatenate)Mix SwapOut !wet
        }
    )Repeat
    (
        20,80,
        {
            !z 
            (?z,6)/ !z
            (>first,1.05)* Prime !first
            (>wet SwapIn,(((?first,30)-,?offset)- Silence,(((?sampleL ReadIn,(-7,?z)-)Volume,?env)Multiply,500,2)ButterworthLowPass)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,(((?first,30)+,?offset)- Silence,(((?sampleN ReadIn,(-4,?z)-)Volume,?env)Multiply,500,2)ButterworthLowPass)Concatenate)Mix SwapOut !wet
            (>wet SwapIn,((?first      ,?offset)- Silence,(((?sampleH ReadIn,(-7,?z)-)Volume,?env)Multiply,500,2)ButterworthLowPass)Concatenate)Mix SwapOut !wet
        }
    )Repeat
    >wet SwapIn
} !reflector
{
    (?pitch,0.95)*,(Random,0.1)*
    (
        ((?length,?pitch)SinWave MakeSquare pcnt+1,?length WhiteNoise pcnt+99)Mix Normalise,
        0.95,
        0.15,
        ?pitch period
    )ResonantFilter Normalise !a
    (
        ?a,
        0.75,
        0.35,
        ?pitch period
    )ResonantFilter Normalise !noise
    (
        (
            ?noise,(?pitch,1.5)*,(?pitch,2.0)*,2
        )BesselBandPass Normalise pcnt+50,
        (
            ?noise,(?pitch,1.2)*,(?pitch,2.0)*,3
        )BesselBandPass Normalise pcnt+50,
    )Mix Normalise !noise        
    (>noise,(?pitch,8)*,1)ButterworthLowPass Normalise !noise
    (>noise, 100,4)ButterworthHighPass Normalise !noise
    (>noise,2000,6)ButterworthLowPass  Normalise !noise
} !make-noise
{
    (?make-noise,,,,,,,,,,)DoAll Mix Normalise !noise
    
    (
        >noise,
        ((0,0),(1000,1),((?length,2)/,0.5),((?length,50)-,0.5),(?length,0))NumericShape
    )Multiply !feed

    [ ae as in bat ]
     660 !f1
    1720 !f2
    2410 !f3
    ((?f1,0.8)*,(?f1,0.8)/,3)CreateButterworthBandPass !fl1
    ((?f2,0.8)*,(?f2,0.8)/,3)CreateButterworthBandPass !fl2
    ((?f3,0.8)*,(?f3,0.8)/,4)CreateButterworthBandPass !fl3
    0.5 !d
    (?feed,0.95,0.05,(>f1 Period,?d)/,>fl1)FilteredResonantFilter !s1
    (?feed,0.95,0.05,(>f2 Period,?d)/,>fl2)FilteredResonantFilter !s2
    (?feed,0.95,0.05,(>f3 Period,?d)/,>fl3)FilteredResonantFilter !s3
    
    (?feed dbs-9,?s1,?s2 ,?s2 dbs+6)Mix Normalise !signal 
    (?signal,(?pitch,10)*,3)ButterworthLowPass Normalise !signal
    (((0,1),((?signal Length,0.9)*,1),(?signal Length,0))NumericShape,>signal)Multiply Normalise
}!nasal

{
    ?length WhiteNoise !noise
    (>pitch,((0.1,Random)*,0.95)+)* !pitch
    (?noise ,?pitch,0.5,36)RBJPeaking Normalise  !signal
    (?signal,?pitch,0.5,48)RBJPeaking Normalise  !signal
    (?signal,?pitch,0.5,48)RBJPeaking Normalise  !signal
    (?signal,?pitch,1.5,48)RBJPeaking Normalise  !signal
    
    (>f1,((0.2,Random)*,0.9)+)* !f1
    (>f2,((0.2,Random)*,0.9)+)* !f2
    (>f3,((0.2,Random)*,0.9)+)* !f3
    (?signal,?f1,0.5,48)RBJPeaking  Normalise  !signal1
    (?signal1,?f1,0.5,48)RBJPeaking Normalise  !signal1
    (?signal1,?f1,0.2,48)RBJPeaking Normalise  !signal1
    
    (?signal,?f2,1.0,48)RBJPeaking  Normalise  !signal2
    (?signal2,?f2,0.5,48)RBJPeaking Normalise  !signal2
    (?signal2,?f2,0.5,48)RBJPeaking Normalise  !signal2
    
    (?signal,?f3,0.5,48)RBJPeaking  Normalise  !signal3
    (?signal3,?f3,0.3,48)RBJPeaking Normalise  !signal3
    (?signal3,?f3,0.1,48)RBJPeaking Normalise  !signal3
    
    (>signal1,>signal2 dbs-6 ,>signal3 dbs-12)Mix Normalise !signal
    (?signal,?pitch,4,12)RBJPeaking Normalise  !signal

}!make-sing

10000 !length
"e4b" Note   !pitch
{
    (?make-sing,,,,,,,,,,,,,,,,)DoAll Mix Normalise !signal

    (
        ((0,0),(1000,1),(((?length,3)/,2000)Min,0.5),((?length,2)/,0.25),(?length,0))NumericShape,
        >signal
    )Multiply Normalise !signal
}!mystic

 660 !f1
1800 !f2
2800 !f3
10000 !length
"e4b" Note   !pitch 
?mystic Do !s1

"g4" Note   !pitch 
?mystic Do !s2

"b4b" Note   !pitch 
?mystic Do !s3

 850 !f1
1200 !f2

"e4b" Note   !pitch 
?mystic Do !s4

"g4" Note   !pitch 
?mystic Do !s5

"b4b" Note   !pitch 
?mystic Do !s6

20000 !length
"e5b" Note   !pitch 
?mystic Do !s7

(
    (?s1 Done !s1,    0),
    (?s2 Done !s2, 5000),
    (?s3 Done !s3,10000),
    (?s1         ,15000),
    (?s4 Done !s4,20000),
    (?s5 Done !s5,25000),
    (?s6 Done !s6,30000),
    (?s7 Done    ,35000)
)MixAt Normalise !signal-base1 Length !length-base1

"b3b" Note !pitch 
?nasal Do  !n1
20000 !length
"b4b" Note !pitch 
?mystic Do !ns1 
10000 !length

"d3" Note  !pitch 
?nasal Do  !n2

"g4" Note  !pitch 
?nasal Do  !n3

20000 !length
"g5" Note  !pitch 
?mystic Do !ns3 
10000 !length

"f4" Note  !pitch 
?nasal Do  !n3a

"e2b" Note !pitch 
?nasal Do !n4
20000 !length
"e3b" Note  !pitch 
?mystic Do !ns4 
10000 !length

"b3b" Note   !pitch 
?nasal Do !n5

"c3" Note   !pitch 
?nasal Do !n6
20000 !length
"c4" Note  !pitch 
?mystic Do !ns6 
10000 !length

20000 !length
"b2b" Note   !pitch 
?nasal Do !n7

(
    (?n1 Done  !n1,          0),
    (?n2 Done  !n2,       5000),
    (?n3 Done  !n3,      10000),
    (?n3a Done !n3a,     15000),
    (?n4 Done  !n4,      20000),
    (?n5 Done  !n5,      25000),
    (?n6 Done  !n6 dbs-3,30000),
    (?n7 Done  !n7,      35000)
)MixAt Normalise !signal-base2 Length !length-base2


(
    (?n1           ,          0),
    (?ns1 Done  !n1,          0),
    (?n2           ,       5000),
    (?n3           ,      10000),
    (?ns3 Done !ns3,      15000),
    (?n3a          ,      15000),
    (?n4           ,      20000),
    (?ns4 Done !ns4,      20000),
    (?n5           ,      25000),
    (?n6      dbs-3,      30000),
    (?ns6 Done !ns6 dbs-3,30000),
    (?n7       !n7,       35000)
)MixAt Normalise !signal-base4 Length !length-base4

 440 !f1
1000 !f2
2250 !f3
10000 !length
"e4b" Note   !pitch 
?mystic Do !s1

"g4" Note   !pitch 
?mystic Do !s2

"b4b" Note   !pitch 
?mystic Do !s3

 300 !f1
 850 !f2

"e4b" Note   !pitch 
?mystic Do !s4

"g4" Note   !pitch 
?mystic Do !s5

"b4b" Note   !pitch 
?mystic Do !s6

20000 !length
"a4" Note   !pitch 
?mystic Do !s7

"e3b" Note   !pitch 
?mystic Do !s8

(
    (?s1 Done !s1,    0),
    (?s2 Done !s2, 5000),
    (?s3 Done !s3,10000),
    (?s1         ,15000),
    (?s4 Done !s4,20000),
    (?s5 Done !s5,25000),
    (?s6 Done !s6,30000),
    (?s7 Done    ,35000),
    (?s8 Done dbs+6,40000)
)MixAt Normalise !signal-base5 Length !length-base5

((0,2),(?length-base1,0.5))NumericShape !shape
(?signal-base1,0.999,0.01,"c4" Note Period,>shape)ShapedResonantFilter Normalise !signal
((0,"e5b" Note),(?length-base1,"e6b" Note))NumericShape !cutoff
((0,0.75),(?length-base1,0.9))NumericShape              !res
(
    ?signal-base1 pcnt+50,
    (?signal,?cutoff,?res)ShapedLadderLowPass pcnt+50
)Mix !signal
(
    ((1,0),((?length-base1,0.9)*,1),(?length-base1,0))NumericShape,
    >signal
)Multiply Normalise !signal-base3
(
    (?signal-base1,0),
    (?signal-base2,50000),
    (?signal-base3,100000),
    (?signal-base3 Reverse,150000),
    (?signal-base4,205000),
    (
        (
            ?signal-base5,
            (((?signal-base5 length WhiteNoise ,"b2b" Note,4,96)RBJPeaking)Mix,500,6)ButterworthLowPass Normalise
            )Mix Normalise ClipToSafe,
            255000
    )
    ,
    (
        (
            ?signal-base2,
            (((?signal-base2 length WhiteNoise ,"b2b" Note,4,96)RBJPeaking)Mix,500,6)ButterworthLowPass Normalise
        )Mix Normalise ClipToSafe,
        310000
    )
           
)MixAt Normalise !signal
(
    ((0,1),((?signal Length,5000)-,1),(?signal Length,0))NumericShape,
    >signal
)Multiply !signal


(80 Silence,>signal !right)Concatenate !left
(?left,1.0004)DirectResample           !left

80 !first
0   !offset
>left !sample
?reflector Do !left
>right !sample
?reflector Do !right

((>left Done Normalise,>right Done Normalise),"temp/tone.wav")WriteFile32