500  !plen
"E6" Note !pitch
{
(?plen,?pitch)SinWave !ping
((0,0),((?plen,10)/,1.5),((?plen,0.4)*,0.8),(?plen,0))NumericShape !nshape
(>ping,>nshape)Multiply !ping

(10,?plen)* !slen
(?plen,10)* WhiteNoise  !sing
((0,0),((?plen,10)/,1.0),(?slen,0))NumericShape !sshape
(?sing,>sshape)Multiply !sing
(?sing,?pitch,2)ButterworthLowPass !sing

((0,64),((?plen,10)/,880),(?slen,32))NumericShape !cuttoff-env
((0,0.3),((?slen,0.5)*,0.7),((?slen,0.8)*,0.8),(?slen,1))NumericShape !resonance-env
(
    (?sing,?cuttoff-env,?resonance-env)ShapedLadderLowPass,
    ?sing
)Mix Normalise !sing 

(
    (
        -0.03,0.2,0,-1,0.2,2,
        ?ping
    )WaveShaper,
    ?sing pcnt+10
)Mix Normalise !signal

[Go stereo]
>signal !left !right

[Simple Reflection ]
(
1,10,
{
(
    (?left        ,0),
    (?left pcnt-25,250),
    (>left pcnt+5 ,520)   [sound to right - other ear]
)MixAt Normalise !left

(
    (?right         ,0),
    (?right  pcnt-5 ,270), [sound to left - other ear]
    (>right pcnt+10,500)
)MixAt Normalise !right
(>right,((0.2,random)*,0.9)+)DirectRelength !right
(>left ,((0.2,random)*,0.9)+)DirectRelength !left
}
)Repeat
(>left,>right)
} !sound

1 Silence !left !right
bunch !dos
(
    1,32,
    {
        (?sound Do,>Dos)AddEnd !dos 
    }
)Repeat

0 !pl !pr
(
    >dos,
    {
        ^l ^r
        (
            (>left,0),
            (?l,?pl)
        )MixAt !left
        (
            (>right,0),
            (?r,?pr)
        )MixAt !right
        
        ((>l length,>r length)+,2)/ !len
        (>pl,(?len,4)/)+ !pl
        (>pr,(?len,4)/)+ !pr
    }
)InvokeAll

((>left Normalise,>right Normalise),"temp/out.wav")WriteFile32
