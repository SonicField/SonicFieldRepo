[ Read the input ]
"temp/tone-a.wav"      !wavFile
?wavFile ReadFile GetStart !dry

[ Model resonance in strings ]
(
	?dry,
	(
		(-16.0,(1000,"E2" Note)/),
		(-16.0,(1000,"A3" Note)/),
		(-16.0,(1000,"D3" Note)/),
		(-16.0,(1000,"G3" Note)/),
		(-16.0,(1000,"B4" Note)/),
		(-16.0,(1000,"E4" Note)/)
	)
) DirectResonate Normalise !wet

(>wet,3000,2)DirectLowPass !wet
(>wet,1000,1)DirectLowPass !wet

(
	>wet pcnt+75,
	>dry pcnt+25
)Mix !signal

[ Hum ]
(?signal length,50)SinWave !hum
(
	>signal Pcnt+99,
	>hum    Pcnt+1
)Mix !signal

[ Delay ]
(
	?signal,
	(
		(-9,80),
	)
) DirectResonate Normalise !wet

[(
	>wet    Pcnt+50,
	?signal Pcnt-50
)Mix !wet
(>wet,880,3)DirectLowPass !wet]

[ Filter ]
(
	>wet    pcnt+25,
	>signal pcnt+75
)Mix !signal

[ Sub Octave ]
[(	
	?signal SubOctave pcnt+10,
	>signal           pcnt+90
)Mix Normalise !signal
]

[ Do some ditortion ]
?signal !distort

(>distort, 6000,2)DirectLowPass Normalise !distort

[ Octave Fuzz - Big Fat Dollops of it ]
(
	(
		?distort Rectify !r Normalise,
		0.1002
	)DirectResample                Pcnt+50,
	(
		>r  Rectify 
		    Normalise dbs+12 Clip,
		0.998
	)DirectResample                Pcnt-50,
	>distort                       Pcnt+10
)Mix Normalise !distort

[ Final Mix ]    
(
   >distort pcnt+65,
   >signal  pcnt+35,
)Mix Normalise  !signal

[ Over clip to remove plucks ]
>signal dbs+6 Clip !signal

[ Soft Clip - like an over driven valve amp ]
[ Over gain ]
(
    1,4,
    {
        (
             0,0.59,0,-1.2,0.25,1,>signal
        ) WaveShaper Normalise    !signal
    }
)Repeat

[ Compress ]

(1000 Silence,>signal,1000 Silence)Concatenate !signal		
(
   1,2,
   {
		(?signal Rectify Reverse,50,2)DirectLowPass Reverse !shape
	 	(1.0,(>shape,0.25)Power dbs+12)DirectMix            !shape
		(
			>signal,
			>shape 
		)Divide Normalise !signal
   }
)Repeat
(1000,(?signal Length,1000)-,>signal)Cut !signal

[DC Filter]
(?signal,50,1)DirectHighPass  !signal
>signal Normalise             !tune

(>tune,110,6000,1 )DirectBandPass Normalise !tune
(>tune,220,10000,1)DirectBandPass Normalise !tune
[(>tune, 6000,2)DirectLowPass     Normalise !tune
(>tune,10000,3)DirectLowPass     Normalise !tune
]

[ Get the results of the tasks and make a wav file ]
((?tune Monitor),"temp/tone-b.wav")WriteFile32
