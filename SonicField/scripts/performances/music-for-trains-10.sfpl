{
    ?f               !pitch
      0              !attack
     75              !decay
     ?length         !sustain
     (?length,4)*    !release
     2.0             !hit-strength
     0.50            !reverb-amount
     2               !damping
          
     (?pitch,?attack,?decay,?sustain,?release,?volume,?hit-strength,?reverb-amount)Println
     ((50,Random)* Silence,?direct-piano Do)Concatenate !a

     (?f,3.0)*       !pitch
      0              !attack
     25              !decay
     (?length,2)/    !sustain
      ?length        !release
     1.0             !hit-strength
     1               !damping
     (?volume,0.33)* !volume
          
     (?pitch,?attack,?decay,?sustain,?release,?volume,?hit-strength,?reverb-amount)Println
     ((50,Random)* Silence,?direct-piano Do)Concatenate !b
     (>a,>b)Mix
}!make-note

{
    bunch !notes-left
    bunch !notes-right
    
    (
        1,?nNotes,
        {
            !x
            ?x println
            {             
                ?x
                ((?x,13)|,?x)+       !x
                (1,(?x, 7)|)+        !a
                (1,(?x,11)|)+        !b
                ((?x, 3)|,2)*        !c
                (((1,?x)+, 3)|,2)*   !d
                (?root,?a)*          !fa        !root-pitch
                (?root,?b)*          !fb
                1.1                  !expand
                Random               !v-left
                (1,?v-left)-         !v-right
                0.75 !volume
                >fa !f ?make-note Do !nal !nar
                0.35 !volume
                >fb !f ?make-note Do !nbl !nbr
                {
                    (>nal,?v-left )NumericVolume !nal
                    (>nar,?v-right)NumericVolume !nar
                    (>nbl,?v-left )NumericVolume !nbl
                    (>nbr,?v-right)NumericVolume !nbr
                }!n-lr-split
                ?n-lr-split invoke
                (
                    ((>nal ,?c)Volume,0),
                    (>nbl  ,?length)
                )MixAt !left
                (
                    ((>nar ,?c)Volume,0),
                    (>nbr  ,?length)
                )MixAt !right
                
                (1,(7,(?x,7)|)-)+   !a
                (1,(11,(?x,11)|)-)+ !b
                (?root,?a)*   !fa        
                (?root,?b)*   !fb
                0.60 !volume
                >fa !f ?make-note Do !nal !nar
                0.35 !volume
                >fb !f ?make-note Do !nbl !nbr
                ?n-lr-split invoke  
                {                  
                    (
                        (>left,0),
                        (>nal ,(?length,2)*),
                        (>nbl ,(?length,3)*)
                    )MixAt Normalise pcnt+35
                }Do !left
                {                  
                    (
                        (>right,0),
                        (>nar ,(?length,2)*),
                        (>nbr ,(?length,3)*)
                    )MixAt Normalise pcnt+15
                }Do !right
                (>left,>right) 
            }Do ^left ^right
            ((?length,2)*,?at)+ !newAt
            (>left , ((35,Random)*,?newAt)+) !element
            (>element,>notes-left )AddEnd !notes-left
            (>right, ((35,Random)*,?newAt)+) !element
            (>element,>notes-right)AddEnd !notes-right
[
     Paramters
     =========
      32    !pitch
     512    !a
    7000    !d  0.5   !dv
    8096    !s  0.5   !sv
    9990    !r
      16    !harmonics
     1.1    !damping
    1.001   !expand
]
            (
                ((?x,2)|,1)eq,
                {
                    {
                         ?x
                        ((?x,13)|,?x)+       !x
                        (1,(?x, 7)|)+        !a
                        (?root,?a)*          !root-pitch
             
                        (?root-pitch,8)/ !pitch
                        (
                            (?pitch,128)gt,
                            (?pitch,2)/,
                            ?pitch
                        )Choose       !pitch
                        
                        (
                            (?pitch,16)lt,
                            (?pitch,2)*,
                            ?pitch
                        )Choose       !pitch
                        
                        (?length,2)/   !a
                        (?length,2)*   !d  0.75   !dv
                        (?length,16)*  !s  0.5   !sv
                        (?length,32)*  !r
                               16      !harmonics
                              2.0      !damping
                             1.001     !expand
                        ("Tame pitch",?pitch)Println
                        ?tame-saw Do
                    }Do !saw              
                    
                    ((?saw pcnt+90,?at      ),>notes-left )AddEnd !notes-left
                    ((?saw        ,(?at,35)+),>notes-right)AddEnd !notes-right
                },{}
            )Choose Invoke
            (>at,(?length,4)*)+ !at
        }
    )repeat
    (
        {>notes-left  MixAt Normalise}Do,
        {>notes-right MixAt Normalise}Do
    )
} !makeTrack

 128   !root
1000   !at
0128   !nNotes
2048   !length

[
?makeTrack Do ^left ^right

[
   Post
   ===
]
"Post Processing" PrintLn
((?left,?right),"temp/temp.wav")WriteFile32 
]

"temp/temp.wav" ReadFile ^left ^right 

8000 !length
{(?length WhiteNoise,128,1)ButterworthHighPass}Do       !convolr
{(>convolr,2.5)Power Normalise}Do                      !convolr
{
    (
        (0,-99),
        ( 10,-20),
        ( 20,  0),
        ( 30,-20),
        (100,-20),
        (125,0),
        (500,-6),
        ((?length,2)/,-20),
        (?length,-60)
    )SimpleShape
}Do  !renvr
{(>convolr Normalise,>renvr)Multiply}Do                             !convolr
{((?length,2)* WhiteNoise,20,1)ButterworthHighPass}Do               !convolrb
{(>convolrb,4.0)Power Normalise}Do                                  !convolrb
{(>convolrb,512,2)ButterworthLowPass Normalise}Do                   !convolrb
(((0,-60),(500,0),((?length,2)*,-60))SimpleShape,>convolrb)Multiply !convolrb
(>convolrb pcnt+20,?convolr pcnt+80)Mix                             !convolr

[ remove infra sound and a bit more]
{(?length WhiteNoise,64,1)ButterworthHighPass}Do       !convoll
{(>convoll,2.5)Power Normalise}Do                      !convoll
{
    (
        (0,-99),
        ( 10,-20),
        ( 25,  0),
        ( 40,-20),
        (100,-20),
        (135,0),
        (450,-6),
        ((?length,2)/,-20),
        (?length,-60)
    )SimpleShape
}Do  !renvl
{(>convoll Normalise,>renvl)Multiply}Do                             !convoll
{((?length,2)* WhiteNoise,20,1)ButterworthHighPass}Do               !convollb
{(>convollb,4.0)Power Normalise}Do                                  !convollb
{(>convollb,400,2)ButterworthLowPass Normalise}Do                   !convollb
(((0,-60),(500,0),((?length,2)*,-60))SimpleShape,>convollb)Multiply !convollb
(>convollb pcnt+20,?convoll pcnt+80)Mix                             !convoll

{((?left pcnt+75,0),(?right pcnt+25,30))MixAt !signal >convoll !convol ?reverb Do Normalise}Do !wleft
{((?left pcnt+25,30),(?right pcnt+75,0))MixAt !signal >convolr !convol ?reverb Do Normalise}Do !wright

{
    (
        {>wleft pcnt+33}Do,
        {>left  pcnt+67}Do
    )Mix Normalise
}Do !left
{
    (
        {>wright pcnt+33}Do,
        {>right  pcnt+67}Do
    )Mix Normalise
}Do !right

[
   Quite bright to start with - be careful 
]
[
0.25 !compress-amount

>left  !signal ?compress-excite Do !left
>right !signal ?compress-excite Do !right
]
((>left,>right),"temp/music-for-trains-10-post.wav")WriteFile32