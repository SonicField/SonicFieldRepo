48000 SetSampleRate
{
    bunch  !sigs
    ?pitch !o-pitch
    (
        1,512,
        {
            !count Println
            (?scale,4)-                     !scale
            (?scale,(9,Random)*)+ Truncate  !scale
            (
                (?scale,1)lt,
                8,
                ?scale
            )Choose                         !scale
            (
                (?scale,128)gt,
                120,
                ?scale
            )Choose                         !scale
            (>pos,(?length,4)*)+            !pos
            {
                (?o-pitch,?scale)*          !freq
                (?freq,?scale)PrintLn
                (5000,(5000,Random)*)+      !len                
                [
                        Parameters 
                        ==========
                        
                        128   !pitch
                        1.2   !pitch-spacing
                          1   !dullness
                        0.15  !ring-amount
                        32    !a
                        128   !d
                        2048  !s
                        8192  !r
                ]
                ?freq                 !pitch
                (1.15,(Random,0.1)*)+ !pitch-spacing
                (1.00,(3,Random)*)+   !dullness
                >dullness Truncate    !dullness
                (0.05,(0.1,Random)*)+ !ring-amount
                32        !a
                128       !d
                (?len,2)/ !s
                ?len      !r
                (?a,?d,?s,?r,?pitch,?pos)Println
                (0.25,(0.75,Random)*)+    !volume
                ((?scale,1)/,-1)* FromDbs !v-correction
                (>volume,?v-correction)*  !volume
                (?a,?d,?s,?r,?pitch,?pos,?v-correction,?volume)Println
                (?liquid-bell Do,>pos)
            }Do !sig
            (>sig,>sigs)AddEnd !sigs
        }
    )Repeat
    Bunch !left
    Bunch !right
    (
        >sigs,
        {
            ^sig ^at
            Random   !vr
            (1,?vr)- !vl
            {(>sig,?vr)NumericVolume}Do !sigr
            {(>sig,?vl)NumericVolume}Do !sigl
            >at                         !at0
            (?at0,(?vr,20)*)+           !at
            ((>sigl,?at),>left)AddEnd   !left
            (?at0,(?vl,20)*)+           !at
            ((>sigr,?at),>right)AddEnd  !right
        }
    )InvokeAll
    {>left  MixAt Normalise}Do !left
    {>right MixAt Normalise}Do !right
    (>left,>right)
}!run

  2048 !length
    32 !pitch0
    
   1000           !pos
?pitch0           !pitch
    64            !scale
?run Do ^left1 ^right1
(?left1 ,"temp/left1" )WriteSignal
(?right1,"temp/right1")WriteSignal
>left1 Final >right1 Final

   1050           !pos
    2            !scale
((?pitch0,4)*,3)/ !pitch
?run Do ^left2 ^right2
(?left2 ,"temp/left2" )WriteSignal
(?right2,"temp/right2")WriteSignal
>left2 Final >right2 Final

   1075           !pos
    16            !scale
((?pitch0,3)*,2)/ !pitch
?run Do ^left3 ^right3
(?left3 ,"temp/left3" )WriteSignal
(?right3,"temp/right3")WriteSignal
>left3 Final >right3 Final

{
    (
        "temp/left1" ReadSignal,
        "temp/left2" ReadSignal,
        "temp/left3" ReadSignal
    )Mix Normalise
}Do !left

{
    (
        "temp/right1" ReadSignal,
        "temp/right2" ReadSignal,
        "temp/right3" ReadSignal
    )Mix Normalise
}Do !right

((>left,>right),"temp/x.wav")WriteFile32

"temp/x.wav" ReadFile ^left ^right

"temp/terrys_warehouse_stereo_short.wav" ReadFile ^convoll ^convolr
>convoll Normalise !convoll
>convolr Normalise !convolr

{?left  !signal >convoll !convol ?reverb Do Normalise}Do !wleft
{?right !signal >convolr !convol ?reverb Do Normalise}Do !wright

{
    (
        >wleft pcnt+50,
        >left  pcnt+50
    )Mix Normalise
}Do !left
{
    (
        >wright pcnt+50,
        >right  pcnt+50
    )Mix Normalise
}Do !right

[
   Quite bright to start with - be careful 
]

((>left,>right),"temp/y.wav")WriteFile32