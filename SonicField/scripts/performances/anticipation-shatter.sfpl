{
    (?sample,1000,5)DirectLowPass  !sampleL
    (?sample,3000,2)DirectLowPass  !sampleN
    (?sample,1000,4)DirectHighPass !sampleH
    (>sampleH,5000,2)DirectLowPass !sampleH
    (?sample,((?first ,?offset)+ Prime Silence,(?sample,-13)Volume)Concatenate)Mix    !wet
    (
        1,2,
        {
            !z PrintLn
            (>first,1.2)* Prime !first
            (?wet,(((?first,30)-,?offset)- Silence,(?sampleL,(-10,?z)-)Volume)Concatenate)Mix   !wet
            (?wet,(((?first,30)+,?offset)- Silence,(?sampleN,(-6, ?z)-)Volume)Concatenate)Mix   !wet
            (?wet,((?first      ,?offset)- Silence,(?sampleH,(-10,?z)-)Volume)Concatenate)Mix   !wet
        }
    )Repeat
    (?sampleL,1000,1)DirectLowPass  !sampleL
    (?sampleN,1000,1)DirectLowPass  !sampleN
    (?sampleH,2000,1)DirectLowPass  !sampleH
    (
        3,28,
        {
            !z PrintLn
            (>first,1.2)* Prime !first
            (?wet,(((?first,30)-,?offset)- Silence,(?sampleL,(-7,?z)-)Volume)Concatenate)Mix    !wet
            (?wet,(((?first,30)+,?offset)- Silence,(?sampleN,(-4,?z)-)Volume)Concatenate)Mix    !wet
            (?wet,((?first      ,?offset)- Silence,(?sampleH,(-7,?z)-)Volume)Concatenate)Mix    !wet
        }
    )Repeat
    >wet
} !reflector

1 Silence !signal
{
    (
        (0,0),
        (25,1),
        (100,1),
        (125,0)
    )NumericShape !env
    (
        0,50,
        {
            !x
            (
                1,60,
                {
                    !y
                    (125,((?y,50)*,((?y,Random)*,50)*)+)SinWave MakeSawTooth !peep
                    (?env,>peep)Multiply !peep
                    (
                        (>signal,0),
                        (>peep,((?x,Random)*,250)*)
                    )MixAt !signal
                }
            )Repeat
        }
    )Repeat
    ?signal Normalise !sample
    ?reflector Do Done
}!shatter

150 !first
0   !offset
?shatter Do !left
?shatter Do !right
((>left Done,>right Done)StereoMonitor,"temp/res.wav")WriteFile32
[
    Mix is in at 2:09.625 
    Also trim off last drum bar from main piece
]